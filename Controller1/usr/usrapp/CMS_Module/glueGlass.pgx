<?xml version="1.0" encoding="utf-8"?>
<Programs xmlns:xsi="http://www.w3.org/2001/XMLSchema-instance" xmlns="http://www.staubli.com/robotics/VAL3/Program/2">
  <Program name="glueGlass">
    <Locals>
      <Local name="nIdx" type="num" xsi:type="array" size="1" />
    </Locals>
    <Code><![CDATA[begin
  while true
    delay(1)
    if bGlueGlass==true
      call selectedCheck()
      call print("starting glueGlass test")
      
      // -- check that relevant vacuum is active
      if !bSelectItem
        // -- check all
        call print("check vacuum on all")
        delay(waitShort)
        for nIdx = 0 to 5
          if statusValves[hdiValve[nIdx]] == 0
            bGlueGlass = false
            call print("Error: no HDI vacuum at position " + toString("1.0", nIdx))
          endIf
          if statusValves[sensorValve[nIdx]] == 0
            bGlueGlass = false
            call print("Error: no sensor vacuum at position " + toString("1.0", nIdx))
          endIf
        endFor
      else 
        // -- check only those that are selected
        call print("check vacuum on selection")
        delay(waitShort)
        for nIdx = 0 to 5
          if statusValves[hdiValve[nIdx]] == 0 and (bSelectedItems[nIdx]==true)
            bGlueGlass = false
            call print("Error: no HDI vacuum at position " + toString("1.0", nIdx))
          endIf
          if statusValves[sensorValve[nIdx]] == 0 and (bSelectedItems[nIdx]==true)
            bGlueGlass = false
            call print("Error: no sensor vacuum at position " + toString("1.0", nIdx))
          endIf
        endFor
      endIf
      if bGlueGlass == false
        call print("Fix missing vacuum and try again")
      endIf
      
    endIf
    
    // -- return above does not work. It terminates the task!
    if bGlueGlass==true
      call setSpeed()
      
      if bSkipGlue[0]==false
        call print("picking module glue stamp")
        // -- pick glue stamp
        call moveDown(moduleGlueStampHome, mFast, trZero, false, trXYZero)
        waitEndMove()
        movel(appro(moduleGlueStampHome, trPressTwo), tTool, mVerySlow)
        waitEndMove()
        delay(waitShort)
        switchMagnet=true
        movel(appro(moduleGlueStampHome, trZsafe), tTool, mSlow)

        call parkEndProgram()

        // -- double-dip stamp into glue before starting the normal procedure
        call print("pre-dip glue stamp")
        call moveDown(glueFirstPoint[1], mFast, trOne, false, trXYZero)
        waitEndMove()
        delay(waitGlue)
        call moveStampUp(glueFirstPoint[1], mSlow, trXYZero)
       
        call moveDown(glueFirstPoint[0], mSlow, trOne, false, trXYZero)
        waitEndMove()
        delay(waitGlue)
        call moveStampUp(glueFirstPoint[0], mSlow, trXYZero)
 
        call parkEndProgram()
        
        // -- apply glue stamp to all positions:
        //    divide approach onto/from glue position into two steps
        //    (1) slowly to trApproach above it
        //    (2) extremely slowly up from glue to trApproach 
        for nIdx=0 to 5
          if !bSelectItem or (bSelectedItems[nIdx]==true)
            call setSpeed()
            if bGlueGlass==true
              call print("getting glue for position " + toString("1.0", nIdx))
              call moveDown(gluePickPoint2[nIdx], mSlow, trOne, false, trXYZero)         
              waitEndMove()
              delay(waitGlue)
              
              call moveStampUp(gluePickPoint2[nIdx], mSlow, trXYZero)

              call parkEndProgram()
              
              // -- move to where the glue should be applied (the final hdi position)
              call print("applying glue to position " + toString("1.0", nIdx))
              call moveDown(hdiFinal[nIdx], mVerySlow, trStamp, false, trXYZero)
              //call moveDown(hdiFinal[nIdx], mVerySlow, trStamp, false, trXYstampOffset)
              waitEndMove()
              delay(waitGlue)
              call moveStampUp(hdiFinal[nIdx], mExtremelySlow, trXYZero)
              //call moveStampUp(hdiFinal[nIdx], mExtremelySlow, trXYstampOffset)
            endIf
          endIf
        endFor

        // -- Drop glue stamp
        call setSpeed()
        call print("put away glue stamp")
        call moveDown(moduleGlueStampEnd, mSlow, trPressTwo, false, trXYZero)
        waitEndMove()
        switchMagnet = false
        movel(appro(moduleGlueStampEnd, trZsafe), tTool, mNomSpeed)
      endIf
      
      if !bSelectItem
        call print("handling all glasses")
      else 
        call print("handling selected glasses")
      endIf
      
      // -- pick weighted vacuum chucks and lift glasses to final locations
      for nIdx=0 to 5
        if !bSelectItem or (bSelectedItems[nIdx]==true)
          call setSpeed()
          if bGlueGlass==true
            // -- move over/to chuck
            call moveDown(chuck2[nIdx], mSlow, trZero, false, trXYZero)
            // -- the following cures the click when the chuck 'jumps' up a bit
            waitEndMove()
            movel(appro(chuck2[nIdx], trPressTwo), tTool, mVerySlow)
            waitEndMove()
         
            call parkEndProgram()
  
            // -- pick chuck and move up
            switchMagnet=true
            delay(waitShort)
            call print("picking module frame " + toString("1.0", nIdx))
            movel(appro(chuck2[nIdx], trZsafe), tTool, mSlow)
          
            call parkEndProgram()
            
            // -- move with chuck to hdi glass(!) position
            call moveDown(glassInitial[nIdx], mSlow, trZero, true, trXYZero)
        
            // -- release hdi vacuum to allow picking up hdi glass
            if statusValves[hdiValve[nIdx]]==1
              call toggleValve(hdiValve[nIdx])
            endIf
            delay(waitShort)
            switchVac0=true
            switchVac1=true
            delay(waitShort)

            // -- move up with hdi glass
            call print("placing hdi glass " + toString("1.0", nIdx))
            movel(appro(glassInitial[nIdx], trZclose), tTool, mSlow)
            movel(appro(glassInitial[nIdx], trZsafe), tTool, mNomSpeed)

            call parkEndProgram()

            // -- move to final HDI position and put down             
            // call moveDown(hdiFinal[nIdx], mVerySlow, trZero, true, trXYZero)
            call moveDown(hdiFinal[nIdx], mVerySlow, trZero, false, trXYCorrectionHDI)
            switchVac0   = false
            switchVac1   = false
            switchMagnet = false

            delay(waitShort)

            // -- move up again
            movel(appro(hdiFinal[nIdx], trZclose), tTool, mSlow)
            movel(appro(hdiFinal[nIdx], trZsafe), tTool, mNomSpeed)

            call parkEndProgram()
          endIf
        endIf
      endFor

      // -- move back somewhere
      call print("parking robot")
      //waitEndMove()
      movej(jInitialPosition, tTool, mFast)
      waitEndMove()
      
      //call selectedClear()
      bGlueGlass=false
      call print("glueGlass done")
    endIf
  endWhile

end]]></Code>
  </Program>
</Programs>